#version 330 core

struct Material {
    sampler2D diffuse;
    sampler2D specular;
    float shininess;
};

struct DirLight {
    vec3 direction;

    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};

struct PointLight {
    vec3 position;

    float constant;
    float linear;
    float quadratic;

    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};

in vec2 TexCoords;
in vec3 FragPos;
in vec3 FragNormal;
out vec4 color;

uniform vec3 viewPos;
uniform DirLight dirLight;
uniform PointLight pointLight;
uniform Material material;

vec3 calcDirLight(DirLight light, vec3 normal, vec3 viewDir);
vec3 calcPointLight(PointLight light, vec3 normal, vec3 viewDir);

void main()
{
    vec3 norm = normalize(FragNormal);
    vec3 viewDir = normalize(viewPos - FragPos);

    // Phase 1: Directional lighting
    vec3 result = calcDirLight(dirLight, norm, viewDir);
    // Phase 2: Point lights
    result += calcPointLight(pointLight, norm, viewDir);

    color = vec4(result, 1.0);
}

vec3 calcDirLight(DirLight light, vec3 normal, vec3 viewDir)
{
    vec3 lightDir = normalize(-light.direction);
    // Diffuse shading
    float diff = max(dot(normal, lightDir), 0.0);

    // Specular shading
    vec3 reflectDir = reflect(-lightDir, normal);
    float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);

    // Combine results
    vec3 diffuseMaterial = vec3(texture(material.diffuse, TexCoords));
    vec3 ambient = light.ambient * diffuseMaterial;
    vec3 diffuse = light.diffuse * diff * diffuseMaterial;
    vec3 specular = light.specular * spec * diffuseMaterial;

    return (ambient + diffuse + specular);
}

vec3 calcPointLight(PointLight light, vec3 normal, vec3 viewDir)
{
    vec3 lightDir = normalize(light.position - FragPos);
    // Diffuse shading
    float diff = max(dot(normal, lightDir), 0.0);
    // Specular shading
    vec3 reflectDir = reflect(-lightDir, normal);
    float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
    // Attenuation
    float d = length(light.position - FragPos);
    bool nolight = (light.constant == 0.0 &&
                    light.linear == 0.0 &&
                    light.quadratic == 0.0);
    float attenuation = nolight ? 0.0 :
        1.0 / (light.constant +
               light.linear * d +
               light.quadratic * (d * d));

    // Combine results
    vec3 diffuseMaterial = vec3(texture(material.diffuse, TexCoords));
    vec3 specularMaterial = vec3(texture(material.specular, TexCoords));
    vec3 ambient = light.ambient * diffuseMaterial;
    vec3 diffuse = light.diffuse * diff * diffuseMaterial;
    vec3 specular = light.specular * spec * specularMaterial;
    ambient *= attenuation;
    diffuse *= attenuation;
    specular *= attenuation;

    return (ambient + diffuse + specular);
}
